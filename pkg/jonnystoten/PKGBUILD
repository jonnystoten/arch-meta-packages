pkgbase=jonnystoten
pkgname=(jonnystoten-base jonnystoten-devel jonnystoten-x jonnystoten-grayson jonnystoten-drake)
pkgver=2019.06.09
pkgrel=1
pkgdesc="Base system configuration for jonnystoten systems"
arch=(any)
license=(MIT)

source=('sudoers.wheel' 'fonts.local.conf')
md5sums=(SKIP SKIP)

package_jonnystoten-base() {
  provides=(vi)
  conflicts=(vi)
  depends=(bash bzip2 coreutils cryptsetup device-mapper dhcpcd diffutils
      e2fsprogs file filesystem findutils gawk gcc-libs gettext glibc grep gzip
      inetutils iproute2 iputils jfsutils less licenses linux logrotate lvm2 man-db
      man-pages mdadm netctl pacman pciutils perl procps-ng psmisc reiserfsprogs
      s-nail sed shadow sysfsutils systemd-sysvcompat tar texinfo usbutils
      util-linux which xfsprogs)
  # removed from base: nano, vi

  depends+=(sudo unzip dialog htop wpa_supplicant openssh)

  install -Dm 0640 sudoers.wheel "$pkgdir/etc/sudoers.d/wheel"

  depends+=(fish stow vim)

  install -dm 0755 "${pkgdir}/usr/bin"
  ln -s /usr/bin/vim "${pkgdir}/usr/bin/vi"
}

package_jonnystoten-devel() {
  install=jonnystoten-devel.install
  depends=(jonnystoten-base)

  # Base Group
  depends+=(autoconf automake binutils bison devtools fakeroot file findutils flex
      gawk gcc gettext grep groff gzip libtool m4 make pacman patch pkg-config
      s3cmd sed sudo systemd texinfo util-linux which)

  depends+=(git hub diff-so-fancy)

  depends+=(docker docker-compose)

  depends+=(inotify-tools)

  depends+=(rustup)

  depends+=(visual-studio-code-bin)
}

package_jonnystoten-x() {
  depends=(jonnystoten-base)

  depends+=(xorg-server xorg-xinit i3-gaps polybar jsoncpp firefox kitty
      redshift python-gobject python-xdg)

  depends+=(pulseaudio pavucontrol spotify-stable mpv youtube-dl)

  # Fonts
  depends+=(ttf-fira-sans ttf-fira-mono ttf-fira-code ttf-font-awesome
      ttf-roboto ttf-roboto-slab ttf-symbola ttf-unifont noto-fonts-emoji
      siji-git)

  install -Dm 0644 fonts.local.conf "$pkgdir/etc/fonts/local.conf"
}

package_jonnystoten-grayson() {
  depends=(jonnystoten-x)

  depends+=(nvidia nvidia-utils ntfs-3g)
}

package_jonnystoten-drake() {
  depends=(jonnystoten-x jonnystoten-devel)

  depends+=(intel-ucode)

  depends+=(terminus-font)
}
